variable "devops-test-cicd-k8s" {
  default = {
    repository = {
      organization    = "pagopa"
      name            = "devops-test-cicd-k8s"
      branch_name     = "master"
      pipelines_path  = ".devops"
      yml_prefix_name = null
    }
    pipeline = {
      enable_code_review = false
      enable_deploy      = true
    }
  }
}

locals {
  # global vars
  devops-test-cicd-k8s-variables = {

  }
  # global secrets
  devops-test-cicd-k8s-variables_secret = {

  }
  # code_review vars
  devops-test-cicd-k8s-variables_code_review = {
    danger_github_api_token = module.secrets_azdo.values["DANGER-GITHUB-API-TOKEN"].value
    sonarcloud_service_conn = "SONARCLOUD-SERVICE-CONN"
    sonarcloud_org          = var.devops-test-cicd-k8s.repository.organization
    sonarcloud_project_key  = "${var.devops-test-cicd-k8s.repository.organization}_${var.devops-test-cicd-k8s.repository.name}"
    sonarcloud_project_name = var.devops-test-cicd-k8s.repository.name
  }
  # code_review secrets
  devops-test-cicd-k8s-variables_secret_code_review = {

  }
  # deploy vars
  devops-test-cicd-k8s-variables_deploy = {
    TF_NAMESPACE                            = local.domain
    TF_DOCKER_IMAGE_NAME                    = var.devops-test-cicd-k8s.repository.name
    TF_CONTAINER_REGISTRY_FQDN_PROD         = local.docker_registry_fqdn_prod
    TF_CONTAINER_REGISTRY_SERVICE_CONN_PROD = local.srv_endpoint_name_docker_registry_prod
    TF_KUBERNETES_SERVICE_CONN_WEU_BETA     = local.srv_endpoint_name_aks_weu_beta_prod
    TF_KUBERNETES_SERVICE_CONN_WEU_PROD_01  = local.srv_endpoint_name_aks_weu_prod01_prod
    TF_KUBERNETES_SERVICE_CONN_WEU_PROD_02  = local.srv_endpoint_name_aks_weu_prod02_prod
    git_email                               = module.secrets_azdo.values["azure-devops-github-EMAIL"].value
    git_username                            = module.secrets_azdo.values["azure-devops-github-USERNAME"].value
    github_connection                       = azuredevops_serviceendpoint_github.azure-devops-github-rw.service_endpoint_name
  }
  # deploy secrets
  devops-test-cicd-k8s-variables_secret_deploy = {

  }
}

module "devops-test-cicd-k8s_code_review" {
  source = "git::https://github.com/pagopa/azuredevops-tf-modules.git//azuredevops_build_definition_code_review?ref=v2.0.4"
  count  = var.devops-test-cicd-k8s.pipeline.enable_code_review == true ? 1 : 0

  project_id                   = data.azuredevops_project.project.id
  repository                   = var.devops-test-cicd-k8s.repository
  github_service_connection_id = azuredevops_serviceendpoint_github.azure-devops-github-pr.id

  pull_request_trigger_use_yaml = true

  variables = merge(
    local.devops-test-cicd-k8s-variables,
    local.devops-test-cicd-k8s-variables_code_review,
  )

  variables_secret = merge(
    local.devops-test-cicd-k8s-variables_secret,
    local.devops-test-cicd-k8s-variables_secret_code_review,
  )

  service_connection_ids_authorization = [
    azuredevops_serviceendpoint_github.azure-devops-github-ro.id,
    # local.azuredevops_serviceendpoint_sonarcloud_id,
  ]
}

module "devops-test-cicd-k8s_deploy" {
  source = "git::https://github.com/pagopa/azuredevops-tf-modules.git//azuredevops_build_definition_deploy?ref=v2.0.4"
  count  = var.devops-test-cicd-k8s.pipeline.enable_deploy == true ? 1 : 0

  project_id                   = data.azuredevops_project.project.id
  repository                   = var.devops-test-cicd-k8s.repository
  github_service_connection_id = azuredevops_serviceendpoint_github.azure-devops-github-pr.id

  ci_trigger_use_yaml = false

  variables = merge(
    local.devops-test-cicd-k8s-variables,
    local.devops-test-cicd-k8s-variables_deploy,
  )

  variables_secret = merge(
    local.devops-test-cicd-k8s-variables_secret,
    local.devops-test-cicd-k8s-variables_secret_deploy,
  )

  service_connection_ids_authorization = [
    azuredevops_serviceendpoint_github.azure-devops-github-ro.id,
    azuredevops_serviceendpoint_github.azure-devops-github-rw.id,
    azuredevops_serviceendpoint_azurecr.acr_docker_registry_prod.id,
    azuredevops_serviceendpoint_kubernetes.aks-weu-beta.id,
    azuredevops_serviceendpoint_kubernetes.aks-weu-prod01.id,
    # azuredevops_serviceendpoint_kubernetes.aks-weu-prod02.id,
  ]
}
